/*
 * Copyright 2019 Cyface GmbH
 *
 * This file is part of the Cyface Utils for Android.
 */
/**
 * Gradle configuration to release this module to Github Packages.
 *
 * @author Armin Schnabel
 * @version 2.0.0
 * @since 1.0.0
 */

apply plugin: 'maven-publish'

// Load credentials
def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

publishing {
    // Authenticate at the repository
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cyface-de/android-utils")
            credentials {
                username = properties.getProperty("github.user")
                password = properties.getProperty("github.token")
            }
        }
    }
    publications {
        Production(MavenPublication) {

            artifact("$buildDir/outputs/aar/utils-release.aar") {
                builtBy tasks.getByName("assemble")
            }
            groupId rootProject.ext.groupId
            artifactId rootProject.ext.artifactId
            version rootProject.ext.version

            pom.withXml {
                // Define this explicitly if using implementation or api configurations
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}