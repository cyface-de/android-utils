/*
 * Copyright 2019 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Gradle's publish file for the utils module.
 *
 * See https://github.com/bintray/gradle-bintray-plugin#readme
 *
 * @author Armin Schnabel
 * @version 1.0.0
 * @since 1.0.0
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version rootProject.ext.version
group rootProject.ext.groupId

publishing {
    publications {
        Production(MavenPublication) {

            artifact("$buildDir/outputs/aar/utils-release.aar") {
                builtBy tasks.getByName("assemble")
            }
            groupId this.group
            artifactId 'android-utils'
            version this.version

            pom.withXml {
                // Define this explicitly if using implementation or api configurations
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'android-utils'
        name = 'de.cyface.utils'
        userOrg = 'cyface-de'
        description = "utility classes required by multiple Cyface Android libraries and projects"
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/cyface-de/android-utils'
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}